plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "org.sonarqube" version "3.3"
    id 'jacoco'
}

group = 'com.eleks.academy'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17

jacoco {
    toolVersion = "0.8.7"
}

sonarqube {
    properties {
        property "sonar.projectKey", "Oleksandr-Eleks_pharmagator"
        property "sonar.organization", "eleks-jc-2021"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.tests", "src/test"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco.xml"
        property "sonar.junit.reportsPath", "build/test-results/*/TEST-*.xml"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.postgresql:postgresql:42.2.24'
    implementation 'org.flywaydb:flyway-core:7.15.0'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'org.modelmapper:modelmapper:2.4.4'
    implementation 'org.apache.poi:poi:5.1.0'
    implementation 'org.apache.poi:poi-ooxml:5.1.0'
    implementation 'com.univocity:univocity-parsers:2.9.1'
    implementation 'com.itextpdf:itextpdf:5.5.13.2'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.dbunit:dbunit:2.7.2'
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.2'
    testImplementation 'org.mockito:mockito-inline:4.1.0'
    testImplementation group: 'io.github.hakky54', name: 'logcaptor', version: '2.7.2'
}

test {
    useJUnitPlatform()
    testLogging {
        events = ["passed", "skipped", "failed"]
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled(true)
        csv.enabled(false)
        html.enabled(true)
        html.destination file("${buildDir}/reports/coverage")
        xml.destination file("${buildDir}/reports/jacoco.xml")
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled(false)
            limit {
                minimum = 0.5
            }
        }
    }
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean

gradle.taskGraph.whenReady {
    taskGraph ->
        if (taskGraph.hasTask(stage)) {
            test.enabled = false
        }
}
